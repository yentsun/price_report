// Product Title Parser

start =
    object*

separated_object =
    separator o:object
        {return o}

object =
    p:percentage {return {percentage: p}}
    / p:package {return {package: p}}
    / cp:composite_package {return {package: cp}}
    / w:word {return {word: w}}
    / t:title {return {title: t}}
    / separated_object

composite_package =
    separator* p1:package plus p2:package {return {amount: p1.amount + p2.amount, unit: p1.unit}}
    / separator* i:integer asterisk p:package {return {amount: i * p.amount, unit: p.unit}}
    / separator* p:package asterisk i:integer separator* pieces {return {amount: i * p.amount, unit: p.unit}}

package =
    a:amount u:unit {return {amount: a, unit: u}}

plus =
    separator* "+" separator*

asterisk =
    separator* "*" separator*
    / separator* "x" separator*

percentage =
   p:amount unit_separator "%" {return p}
   / r:range unit_separator "%" {return r}
   / from min:percentage to max:percentage {return [min, max]}

from =
    separator* "от" separator*

to =
    separator* "до" separator*

range =
    min:amount "-" max:amount {return [min, max]}

amount =
    f:float {return parseFloat(f)}
    / i:integer {return parseInt(i)}

unit_separator =
    " "
    / ""

unit =
    unit_separator u:unit_token
        {return u}

unit_token =
    "l"
    / "л" {return "l"}

    / "ml"
    / "мл" {return "ml"}

    / "g"
    / "гр" {return "g"}
    / "г" {return "g"}

    / "kg"

    / pieces

pieces =
    "pcs"
    / "шт" {return "pcs"}

float =
    i:integer float_separator d:digit+
        {return i + "." + d.join("")}

float_separator =
    "."
    / ","

integer =
    f:[1-9] rest:digit*
        {return ""+f+rest.join("")}
    / "0"

digit =
    [0-9]

title =
    '"' f:word separator* r:word* '"' {return f+" "+r.join(" ")}

word =
    f:word_start r:alpha*
        {return "" + f + r.join("")}

word_start =
    [a-z\u0430-\u044F]

alpha =
    [a-z\u0430-\u044F\\\/]

separator =
    ". "
    / ", "
    / " "
    / "."
